
plugins {
    id 'java'
    id 'war'
    id 'idea'
    id 'jacoco'
    id 'com.bmuschko.tomcat' version "2.0"
    id 'com.github.kt3k.coveralls' version "2.0.1x"
}

sourceCompatibility = 1.8
version = '1.0'

repositories {
    mavenCentral()
}

configurations {
    all*.exclude group: 'junit', module: 'junit'
    all*.exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
}

dependencies {

    def jerseyVersion = '2.13'
    def tomcatVersion = '8.0.15'

    compile "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion",
            "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion",
            'org.glassfish.hk2:guice-bridge:2.4.0-b07',
            'com.google.inject.extensions:guice-servlet:4.0-beta5',
            'org.quartz-scheduler:quartz:2.2.1',
            'org.quartz-scheduler:quartz-jobs:2.2.1',
            'org.jsoup:jsoup:1.7.2',
            'org.elasticsearch:elasticsearch:1.4.2',
            'com.github.jknack:handlebars:2.0.0',
            'org.apache.commons:commons-email:1.3.3',
            'commons-configuration:commons-configuration:1.10',
            'org.slf4j:slf4j-api:1.7.7'

    runtime 'com.h2database:h2:1.4.183',
            'ch.qos.logback:logback-classic:1.1.2'

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile 'org.testng:testng:6.8.8',
                'org.assertj:assertj-core:1.7.0',
                'org.mockito:mockito-all:1.10.8',
                'org.mock-server:mockserver-netty:3.9.1',
                'com.icegreen:greenmail:1.4.0',
                "org.apache.tomcat.embed:tomcat-embed-core:$tomcatVersion"

    testRuntime "org.apache.tomcat.embed:tomcat-embed-logging-juli:$tomcatVersion",
                "org.apache.tomcat.embed:tomcat-embed-jasper:$tomcatVersion"

    tomcat "org.apache.tomcat.embed:tomcat-embed-core:$tomcatVersion",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:$tomcatVersion",
            "org.apache.tomcat.embed:tomcat-embed-jasper:$tomcatVersion"
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
